<?xml version="1.0" encoding="UTF-8"?>
<hdevelop file_version="1.2" halcon_version="18.11.0.1">
<procedure name="main">
<interface/>
<body>
<c>* ball.hdev: 测量焊球的直径</c>
<c></c>
<c>* 读取图片</c>
<l>read_image (Bond, 'die/die_03')</l>
<c></c>
<c>* 初始化窗口显示</c>
<l>dev_update_window ('off')</l>
<l>dev_close_window ()</l>
<l>dev_open_window (0, 0, 728, 512, 'black', WindowID)</l>
<l>dev_display (Bond)</l>
<l>set_display_font (WindowID, 14, 'mono', 'true', 'false')</l>
<l>disp_continue_message (WindowID, 'black', 'true')</l>
<l>stop ()</l>
<c></c>
<c>* 定位芯片（上方空白的大片黑色区域会影响结果）</c>
<l>threshold (Bond, Bright, 100, 255)</l>
<l>shape_trans (Bright, Die, 'rectangle2')</l>
<l>dev_set_color ('green')</l>
<l>dev_set_line_width (3)</l>
<l>dev_set_draw ('margin')</l>
<l>dev_display (Die)</l>
<l>disp_continue_message (WindowID, 'black', 'true')</l>
<l>stop ()</l>
<c></c>
<c>* 提取芯片上的黑色区域</c>
<l>reduce_domain (Bond, Die, DieGrey)</l>
<l>threshold (DieGrey, Wires, 0, 50)</l>
<l>fill_up_shape (Wires, WiresFilled, 'area', 1, 100)</l>
<l>dev_display (Bond)</l>
<l>dev_set_draw ('fill')</l>
<l>dev_set_color ('red')</l>
<l>dev_display (WiresFilled)</l>
<l>disp_continue_message (WindowID, 'black', 'true')</l>
<l>stop ()</l>
<c></c>
<c>* 对这些区域进行开运算，可以去除无关区域</c>
<l>opening_circle (WiresFilled, Balls, 15.5)</l>
<l>dev_set_color ('green')</l>
<l>dev_display (Balls)</l>
<l>disp_continue_message (WindowID, 'black', 'true')</l>
<l>stop ()</l>
<c></c>
<c>* 这里将上面的区域进行了分割，并筛选出圆形区域</c>
<l>connection (Balls, SingleBalls)</l>
<l>select_shape (SingleBalls, IntermediateBalls, 'circularity', 'and', 0.85, 1.0)</l>
<l>sort_region (IntermediateBalls, FinalBalls, 'first_point', 'true', 'column')</l>
<l>dev_display (Bond)</l>
<l>dev_set_colored (12)</l>
<l>dev_display (FinalBalls)</l>
<l>disp_continue_message (WindowID, 'black', 'true')</l>
<l>stop ()</l>
<c></c>
<c>* 计算圆形直径</c>
<l>smallest_circle (FinalBalls, Row, Column, Radius)</l>
<l>NumBalls := |Radius|</l>
<l>Diameter := 2 * Radius</l>
<l>meanDiameter := mean(Diameter)</l>
<l>minDiameter := min(Diameter)</l>
<l>dev_display (Bond)</l>
<l>disp_circle (WindowID, Row, Column, Radius)</l>
<l>dev_set_color ('white')</l>
<l>disp_message (WindowID, 'D: ' + Diameter$'.4', 'image', Row - 2 * Radius, Column, 'white', 'false')</l>
<l>dev_update_window ('on') </l>
</body>
<docu id="main">
<parameters/>
</docu>
</procedure>
</hdevelop>
