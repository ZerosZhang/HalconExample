<?xml version="1.0" encoding="UTF-8"?>
<hdevelop file_version="1.2" halcon_version="18.11.0.1">
<procedure name="main">
<interface/>
<body>
<c>* *********************** 注意 *************************</c>
<c>* </c>
<c>* 在本例之前要先执行 create_model_green_dot.hdev </c>
<c>* 以生成 'green-dot.shm'， 本例中需要用到。</c>
<c>* </c>
<c>* ******************************************************</c>
<c>* 本示例演示了如何使用"create_model_green_dot.hdev"创建的模板搜索绿点。</c>
<c></c>
<c>* 获取图像并初始化显示设置</c>
<l>dev_update_off ()</l>
<l>dev_close_window ()</l>
<l>read_image (ImageSearch, 'green-dots')</l>
<l>dev_open_window_fit_image (ImageSearch, 0, 0, -1, -1, WindowHandle)</l>
<l>set_display_font (WindowHandle, 16, 'mono', 'true', 'false')</l>
<l>dev_set_draw ('margin')</l>
<l>dev_set_line_width (3)</l>
<l>Message := '使用之前保存的模板搜索旋转和缩放的绿点'</l>
<l>disp_message (WindowHandle, Message, 'window', 12, 12, 'black', 'true')</l>
<l>dev_display (ImageSearch)</l>
<l>stop ()</l>
<c>* </c>
<c>* 检查文件是否存在</c>
<l>file_exists ('green-dot.shm', FileExists)</l>
<c>* </c>
<c>* 如果模板文件存在，则读取模板并进行匹配</c>
<c>* 如果模板文件不存在，则显示错误消息</c>
<l>if (FileExists)</l>
<c>    * 读取模板</c>
<l>    read_shape_model ('green-dot.shm', ModelID)</l>
<c>    </c>
<c>    * 匹配模板</c>
<l>    find_scaled_shape_model (ImageSearch, ModelID, rad(-45), rad(90), 0.8, 1.0, 0.5, 0, 0.5, 'least_squares', 5, 0.8, Row, Column, Angle, Scale, Score)</l>
<l>    get_shape_model_contours (ModelContours, ModelID, 1)</l>
<c>    </c>
<c>    * 显示匹配结果</c>
<l>    dev_display (ImageSearch)</l>
<l>    for I := 0 to |Score| - 1 by 1</l>
<l>        vector_angle_to_rigid (0, 0, 0, Row[I], Column[I], Angle[I], HomMat2DRotate)</l>
<l>        hom_mat2d_scale (HomMat2DRotate, Scale[I], Scale[I], Row[I], Column[I], HomMat2DScale)</l>
<l>        affine_trans_contour_xld (ModelContours, ModelTrans, HomMat2DScale)</l>
<l>        dev_display (ModelTrans)</l>
<l>    endfor</l>
<c>    </c>
<c>    * 清理模板</c>
<l>    clear_shape_model (ModelID)</l>
<l>else</l>
<l>    dev_clear_window ()</l>
<l>    Message := '模板文件不存在! \r\n请先执行 create_model_green_dot.hdev 例程以生成模板文件'</l>
<l>    disp_message (WindowHandle, Message, 'window', 12, 12, 'red', 'true')</l>
<l>endif</l>
</body>
<docu id="main">
<parameters/>
</docu>
</procedure>
</hdevelop>
