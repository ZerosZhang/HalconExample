<?xml version="1.0" encoding="UTF-8"?>
<hdevelop file_version="1.2" halcon_version="18.11.0.1">
<procedure name="main">
<interface/>
<body>
<c>* 读取图片，初始化显示窗口</c>
<l>dev_close_window ()</l>
<l>read_image (Image, 'ic_pin')</l>
<l>get_image_size (Image, Width, Height)</l>
<l>dev_open_window (0, 0, Width / 2, Height / 2, 'black', WindowHandle)</l>
<l>set_display_font (WindowHandle, 14, 'mono', 'true', 'false')</l>
<l>dev_display (Image)</l>
<l>disp_continue_message (WindowHandle, 'black', 'true')</l>
<l>stop ()</l>
<c></c>
<c>* 创建测量区域并测量针脚宽度</c>
<l>Row := 47</l>
<l>Column := 485</l>
<l>Phi := 0</l>
<l>Length1 := 420</l>
<l>Length2 := 10</l>
<l>dev_set_color ('green')</l>
<l>dev_set_draw ('margin')</l>
<l>dev_set_line_width (3)</l>
<l>gen_rectangle2 (Rectangle, Row, Column, Phi, Length1, Length2)</l>
<l>gen_measure_rectangle2 (Row, Column, Phi, Length1, Length2, Width, Height, 'nearest_neighbor', MeasureHandle)</l>
<l>measure_pairs (Image, MeasureHandle, 1.5, 30, 'negative', 'all', RowEdgeFirst, ColumnEdgeFirst, AmplitudeFirst, RowEdgeSecond, ColumnEdgeSecond, AmplitudeSecond, PinWidth, PinDistance)</l>
<l>disp_line (WindowHandle, RowEdgeFirst, ColumnEdgeFirst, RowEdgeSecond, ColumnEdgeSecond)</l>
<l>stop ()</l>
<c></c>
<c>* 显示测量结果</c>
<l>dev_set_color ('red')</l>
<l>avgPinWidth := sum(PinWidth) / |PinWidth|</l>
<l>avgPinDistance := sum(PinDistance) / |PinDistance|</l>
<l>numPins := |PinWidth|</l>
<l>dev_set_color ('yellow')</l>
<l>disp_message (WindowHandle, '针脚数量: ' + numPins, 'image', 200, 100, 'yellow', 'false')</l>
<l>disp_message (WindowHandle, '针脚平均宽度:  ' + avgPinWidth, 'image', 260, 100, 'yellow', 'false')</l>
<l>disp_message (WindowHandle, '针脚平均间距:  ' + avgPinDistance, 'image', 320, 100, 'yellow', 'false')</l>
<l>disp_continue_message (WindowHandle, 'black', 'true')</l>
<l>stop ()</l>
<c></c>
<c>* 精细显示，放大图片区域</c>
<l>Row1 := 0</l>
<l>Column1 := 600</l>
<l>Row2 := 100</l>
<l>Column2 := 700</l>
<l>dev_set_color ('blue')</l>
<l>disp_rectangle1 (WindowHandle, Row1, Column1, Row2, Column2)</l>
<l>dev_set_part (Row1, Column1, Row2, Column2)</l>
<l>dev_display (Image)</l>
<l>dev_set_color ('green')</l>
<l>dev_display (Rectangle)</l>
<l>dev_set_color ('red')</l>
<l>disp_line (WindowHandle, RowEdgeFirst, ColumnEdgeFirst, RowEdgeSecond, ColumnEdgeSecond)</l>
<l>disp_continue_message (WindowHandle, 'black', 'true')</l>
<l>stop ()</l>
<c></c>
<c>* 创建测量区域并测量针脚高度，这里的原理也是找边缘对</c>
<l>dev_set_part (0, 0, Height - 1, Width - 1)</l>
<l>dev_display (Image)</l>
<l>disp_continue_message (WindowHandle, 'black', 'true')</l>
<l>dev_set_color ('green')</l>
<l>Row := 508</l>
<l>Column := 200</l>
<l>Phi := -1.5708</l>
<l>Length1 := 482</l>
<l>Length2 := 35</l>
<l>gen_rectangle2 (Rectangle, Row, Column, Phi, Length1, Length2)</l>
<l>gen_measure_rectangle2 (Row, Column, Phi, Length1, Length2, Width, Height, 'nearest_neighbor', MeasureHandle)</l>
<l>measure_pos (Image, MeasureHandle, 1.5, 30, 'all', 'all', RowEdge, ColumnEdge, Amplitude, Distance)</l>
<l>PinHeight1 := RowEdge[1] - RowEdge[0]</l>
<l>PinHeight2 := RowEdge[3] - RowEdge[2]</l>
<l>dev_set_color ('red')</l>
<l>disp_line (WindowHandle, RowEdge, ColumnEdge - Length2, RowEdge, ColumnEdge + Length2)</l>
<l>disp_message (WindowHandle, 'Pin Height:  ' + PinHeight1, 'image', RowEdge[1] + 40, ColumnEdge[1] + 100, 'yellow', 'false')</l>
<l>disp_message (WindowHandle, 'Pin Height:  ' + PinHeight2, 'image', RowEdge[3] - 120, ColumnEdge[3] + 100, 'yellow', 'false')</l>
</body>
<docu id="main">
<parameters/>
</docu>
</procedure>
</hdevelop>
